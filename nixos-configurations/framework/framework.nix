# Do not modify this file!  It was generated by ‘nixos-generate-config’
# and may be overwritten by future invocations.  Please make changes
# to /etc/nixos/configuration.nix instead.
{
  config,
  lib,
  pkgs,
  options,
  modulesPath,
  inputs,
  ...
}:
{
  imports = [
    inputs.nixos-hardware.nixosModules.framework-11th-gen-intel
  ];

  dev.johnrinehart.droidcam.enable = false; # TODO: broken
  dev.johnrinehart.auto-suspend = {
    notificationLevels = builtins.genList (i: 30 - (i * 5)) 6; # [ 30 25 20 15 10 5 ]
    enable = true;
  };

  nix = {
    settings = {
      extra-experimental-features = "nix-command flakes";
      trusted-users = [ "john" ];
    };

    distributedBuilds = true;

    buildMachines = [
      {
        hostName = "100.74.54.40";
        sshUser = "john";
        protocol = "ssh-ng";
        system = "x86_64-linux";
        maxJobs = 12;
        supportedFeatures = [
          "benchmark"
          "big-parallel"
          "gccarch-alderlake"
          "gccarch-x86_64-v3"
          "gccarch-znver1"
          "gccarch-znver3"
          "kvm"
          "nixos-test"
        ];
        publicHostKey = "c3NoLWVkMjU1MTkgQUFBQUMzTnphQzFsWkRJMU5URTVBQUFBSUNuYlFKakdjYUExS1N3LzZSbnFxZzNlNGxwNFJtSFVKb1dpR3NXQk5lNVUgcm9vdEBuaXhvcwo=";
      }
    ];
  };

  boot.supportedFilesystems = [
    "ntfs"
    "exfat"
    "vfat"
    "btrfs"
    "ext"
  ];

  boot.initrd.availableKernelModules = [
    "xhci_pci"
    "thunderbolt"
    "nvme"
    "uas"
    "usb_storage"
    "sd_mod"
  ];

  boot.initrd.kernelModules = [ ];

  fileSystems."/" = {
    device = "/dev/disk/by-uuid/9dbd56c9-2344-450a-845b-6490d0879256";
    fsType = "ext4";
  };

  fileSystems."/boot" = {
    device = "/dev/disk/by-uuid/32F9-A71A";
    fsType = "vfat";
  };

  fileSystems."/mnt/framework250_1" = {
    device = "/dev/disk/by-uuid/caca60c2-483b-4a58-a443-5c8df3b3c82d";
    fsType = "btrfs";
    options = [
      "user"
      "rw"
      "async"
      "auto"
      "nofail"
      "x-systemd.device-timeout=2s"
    ];
  };

  # The global useDHCP flag is deprecated, therefore explicitly set to false here.
  # Per-interface useDHCP will be mandatory in the future, so this generated config
  # replicates the default behaviour.
  networking.hostName = "framie";
  networking.useDHCP = lib.mkDefault false;
  networking.nameservers = [
    "1.1.1.1"
    "8.8.8.8"
    "6.6.6.6"
  ];
  networking.resolvconf.enable = true;
  networking.interfaces.wlp170s0.useDHCP = lib.mkDefault true;

  powerManagement.cpuFreqGovernor = lib.mkDefault "powersave";
  hardware.cpu.intel.updateMicrocode = lib.mkDefault config.hardware.enableRedistributableFirmware;
  hardware.enableRedistributableFirmware = true;

  security.rtkit.enable = true;

  # bluetooth stuff
  services.blueman.enable = true;
  hardware = {
    bluetooth.enable = true;
  };

  ## Below v4l2loopback stuff stolen from https://gist.github.com/TheSirC/93130f70cc280cdcdff89faf8d4e98ab
  # Extra kernel modules
  boot.kernelPackages = pkgs.linuxPackages_latest;
  boot.extraModulePackages = [ config.boot.kernelPackages.v4l2loopback ];

  environment.etc."modprobe.d/v4l2loopback.conf".text = ''
    options v4l2loopback video_nr=0,1,2 card_label="Virtual Video 0,Virtual Video 1,Virtual Video 2" exclusive_caps=1
  '';

  environment.etc."modules-load.d/v4l2loopback.conf".text = ''
    v4l2loopback
  '';

  boot.kernelModules = [
    "kvm-intel" # detected automatically
  ];

  services.chrony = {
    enable = true;
    # if we're off by >1s then jump system clock only within the 1st 3 clock
    # updates
    extraConfig = ''
      makestep 1 3
    '';
  };

  services.tailscale.enable = true;
  networking.firewall.checkReversePath = "loose";

  # Fix tailscaled hanging during shutdown when trying to cleanup UPnP port mappings
  # The daemon has an internal 45s watchdog timeout when closing, which blocks shutdown.
  # It tries to delete port mappings from the router, but this hangs if the network
  # is going down or the router is unresponsive.
  systemd.services.tailscaled = {
    # Stop tailscale BEFORE network goes down during shutdown
    # Only use network.target to avoid ordering cycles with NetworkManager
    before = [ "network.target" ];

    serviceConfig = {
      # Gracefully disconnect from tailnet before stopping the daemon
      # This avoids hanging on UPnP port mapping cleanup during shutdown
      ExecStop = "${pkgs.tailscale}/bin/tailscale down --accept-risk=all";

      # Still set a timeout as a safety measure
      # Should complete quickly now that we disconnect first
      TimeoutStopSec = "2s";

      # Send SIGTERM first, then SIGKILL after timeout
      KillMode = "process";
    };
  };

  # Separate service to bring tailscale up after network is online
  # This avoids ExecStartPost timing out when network isn't ready at boot
  systemd.services.tailscale-autoconnect = {
    description = "Automatically connect to Tailscale";
    after = [
      "network-online.target"
      "tailscaled.service"
    ];
    wants = [ "network-online.target" ];
    wantedBy = [ "multi-user.target" ];

    serviceConfig = {
      Type = "oneshot";
      ExecStart = "${pkgs.tailscale}/bin/tailscale up --accept-routes --accept-dns=false";
      RemainAfterExit = true;

      # Prevent blocking boot if network is slow/unavailable
      TimeoutStartSec = "30s";

      # Retry if it fails (e.g., network not ready yet)
      Restart = "on-failure";
      RestartSec = "10s";
    };
  };

  networking.timeServers = options.networking.timeServers.default ++ [ "time.facebook.com" ];

  programs.noisetorch.enable = true;

  networking.networkmanager.enable = true;
  systemd.services.NetworkManager-wait-online.enable = false;

  services.upower = {
    enable = true;
  };

  swapDevices = [
    {
      device = "/dev/nvme0n1p3";
    }
  ];

  # Decent value for SSD (writes are expensive - chew up lifetime)
  boot.kernel.sysctl."vm.swappiness" = 10;

  systemd.sleep.extraConfig = ''
    # https://github.com/systemd/systemd/blob/595d88cdc86afdf40127282d711c5985c85fed9b/src/shared/sleep-config.c#L96-L97
    # SuspendState=disk mem freeze
    SuspendState=mem freeze
    MemorySleepMode=deep s2idle
  '';

  boot.kernelParams = [
    "resume=/dev/nvme0n1p3"
    "nocompress"
    "mem_sleep_default=deep"
  ];

  boot.resumeDevice = "/dev/nvme0n1p3";

  services.logind.settings.Login = {
    SleepOperation = "suspend-then-hibernate hibernate hybrid-sleep suspend";
    HandleLidSwitch="hibernate";
    HandlePowerKey="hibernate";
    HandleSuspendKey="hibernate";
  };

  services.getty.loginOptions = "-- \\u TMOUT=10";

  dev.johnrinehart.sound.enable = true;

  virtualisation.vmVariant = ({ ... }: {
    fileSystems."/" = {
      device = "/dev/sda";
      fsType = "ext4";
    };

    boot.resumeDevice = lib.mkForce "";
    swapDevices = lib.mkForce [];

    networking.interfaces = lib.mkForce {};
  });

  hardware.keyboard.zsa.enable = true;
}
